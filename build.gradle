plugins {
    id "de.undercouch.download" version "3.4.3"
}

group = 'com.orientechnologies'
version = '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile group: 'com.orientechnologies', name: 'orientdb-core', version: '3.0.2-SNAPSHOT'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}


task downloadProfiles(type: Download) {
    src 'https://snap.stanford.edu/data/soc-pokec-profiles.txt.gz'
    dest rootDir
    overwrite false
}

task downloadRelations(type: Download) {
    src 'https://snap.stanford.edu/data/soc-pokec-relationships.txt.gz'
    dest rootDir
    overwrite false
}

task pokecLoad(type: JavaExec, dependsOn: [classes, downloadProfiles, downloadRelations]) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.orientechnologies.pokec.load.PokecLoad'
    jvmArgs = ["-server"]
    args = extractArgs()
}

task pokecRead(type: JavaExec, dependsOn: classes) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.orientechnologies.pokec.read.PokecRead'
    jvmArgs = ["-server"]
    args = extractArgs()
}

task pokecUpdate(type: JavaExec, dependsOn: classes) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.orientechnologies.pokec.contentupdate.PokecUpdate'
    jvmArgs = ["-server"]
    args = extractArgs()
}

def extractArgs() {
    def args = []
    def properties = ["embedded", "engineDirectory", "dbName", "remoteURL", "numThreads", "indexType"]

    properties.each { property ->
        if (project.hasProperty(property)) {
            args.add("-" + property)
            args.add(project.property(property))
        }
    }

    return args
}

